/*
 * Skytils - Hypixel Skyblock Quality of Life Mod
 * Copyright (C) 2021 Skytils
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id "net.minecraftforge.gradle.forge" version "6f5327"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "org.spongepowered.mixin" version "d5f9873d60"
}

version = "1.0-pre19.1"
group = "skytils.skytilsmod"
archivesBaseName = "Skytils"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    gitVersion = false
    clientJvmArgs += "-Dfml.coreMods.load=skytils.skytilsmod.tweaker.SkytilsLoadingPlugin"
    clientJvmArgs += "-Delementa.dev=true"
    clientJvmArgs += "-Delementa.debug=true"
    clientRunArgs += "--tweakClass org.spongepowered.asm.launch.MixinTweaker"
    clientRunArgs += "--mixin mixins.skytils.json"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url "https://repo.sk1er.club/repository/maven-public/" }
    maven { url "https://repo.sk1er.club/repository/maven-releases/" }
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }

    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    implementation("gg.essential:vigilance-1.8.9-forge:153") {
        exclude module: "kotlin-reflect"
        exclude module: "kotlin-stdlib-jdk8"
    }

    implementation "org.apache.httpcomponents.client5:httpclient5:5.1"
    implementation("com.github.Skytils:Hylin:dc8519502e") {
        exclude module: "kotlin-reflect"
        exclude module: "kotlin-stdlib-jdk8"
        exclude module: "kotlinx-coroutines-core"
    }

    implementation platform("org.jetbrains.kotlin:kotlin-bom")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1-native-mt"
}

jar {
    manifest.attributes(
            "Main-Class": "SkytilsInstallerFrame",
            "FMLCorePlugin": "skytils.skytilsmod.tweaker.SkytilsLoadingPlugin",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.skytils.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
    )

    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    relocate("gg.essential", "skytils.essentialgg")
    relocate("kotlinx.coroutines", "skytils.kotlinx.coroutines")
    relocate("org.apache.hc", "skytils.apacheorg.hc")
    relocate("org.apache.commons.codec", "skytils.apacheorg.commons.codec")

    exclude "**/LICENSE.md"
    exclude "**/LICENSE.txt"
    exclude "**/LICENSE"
    exclude "**/NOTICE"
    exclude "**/NOTICE.txt"
    exclude "pack.mcmeta"
    exclude "dummyThing"
    exclude "**/module-info.class"

    exclude "META-INF/proguard/**"
    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
    exclude "META-INF/com.android.tools/**"

    exclude "fabric.mod.json"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.skytils.refmap.json"
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.skytils.refmap.json"
        output.resourcesDir = file("${buildDir}/classes/kotlin/main")
    }
}

tasks.reobfJar.dependsOn(shadowJar)
